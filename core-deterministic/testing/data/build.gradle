apply plugin: 'kotlin'

configurations {
    testData
}

dependencies {
    testCompile project(':core')
    testCompile project(':finance')
    testCompile project(':node-driver')
    testCompile project(path: ':core-deterministic:testing:verifier', configuration: 'runtimeArtifacts')

    testCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile "org.jetbrains.kotlin:kotlin-reflect"
    testCompile "junit:junit:$junit_version"
}

jar.enabled = false

test {
    filter {
        // Running this class is the whole point, so include it explicitly.
        includeTestsMatching "net.corda.deterministic.data.GenerateData"
    }
    // force execution of these tests to generate artifacts required by other module (eg. VerifyTransactionTest)
    // note: required by Gradle Build Cache.
    outputs.upToDateWhen { false }
}
assemble.finalizedBy test

artifacts {
    testData file: file("$buildDir/test-data.jar"), type: 'jar', builtBy: test
}
